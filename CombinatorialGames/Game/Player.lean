/-
Copyright (c) 2025 Yuyang Zhao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuyang Zhao
-/
import Mathlib.Algebra.Ring.Defs
import Mathlib.Data.Fintype.Defs
import Mathlib.Logic.Small.Defs
import Mathlib.Order.Antisymmetrization
import Mathlib.Tactic.DeriveFintype

/-!
# Type of players

This file implements the two-element type of players (`Left`, `Right`), alongside other basic
notational machinery to be used within game theory.
-/

universe u

/-! ### Players -/

/-- Either the Left or Right player. -/
@[aesop safe cases, grind cases]
inductive Player where
  /-- The Left player. -/
  | left  : Player
  /-- The Right player. -/
  | right : Player
deriving DecidableEq, Fintype, Inhabited

namespace Player

/-- Specify a function `Player → α` from its two outputs. -/
@[simp]
abbrev cases {α : Sort*} (l r : α) : Player → α
  | left => l
  | right => r

lemma apply_cases {α β : Sort*} (f : α → β) (l r : α) (p : Player) :
    f (cases l r p) = cases (f l) (f r) p := by
  cases p <;> rfl

@[simp]
theorem cases_inj {α : Sort*} {l₁ r₁ l₂ r₂ : α} :
    cases l₁ r₁ = cases l₂ r₂ ↔ l₁ = l₂ ∧ r₁ = r₂ :=
  ⟨fun h ↦ ⟨congr($h left), congr($h right)⟩, fun ⟨hl, hr⟩ ↦ hl ▸ hr ▸ rfl⟩

theorem const_of_left_eq_right {α : Sort*} {f : Player → α} (hf : f left = f right) :
    ∀ p q, f p = f q
  | left, left | right, right => rfl
  | left, right => hf
  | right, left => hf.symm

theorem const_of_left_iff_right {f : Player → Prop} (hf : f left ↔ f right) (p q) : f p ↔ f q :=
  (const_of_left_eq_right hf.eq ..).to_iff

@[simp]
protected lemma «forall» {p : Player → Prop} :
    (∀ x, p x) ↔ p left ∧ p right :=
  ⟨fun h ↦ ⟨h left, h right⟩, fun ⟨hl, hr⟩ ↦ fun | left => hl | right => hr⟩

@[simp]
protected lemma «exists» {p : Player → Prop} :
    (∃ x, p x) ↔ p left ∨ p right :=
  ⟨fun | ⟨left, h⟩ => .inl h | ⟨right, h⟩ => .inr h, fun | .inl h | .inr h => ⟨_, h⟩⟩

instance : Neg Player where
  neg := cases right left

@[simp, grind =] lemma neg_left : -left = right := rfl
@[simp, grind =] lemma neg_right : -right = left := rfl
@[simp] theorem eq_neg : ∀ {p q : Player}, p = -q ↔ p ≠ q := by decide
@[simp] theorem neg_eq : ∀ {p q : Player}, -p = q ↔ p ≠ q := by decide
theorem ne_neg : ∀ {p q : Player}, p ≠ -q ↔ p = q := by decide
theorem neg_ne : ∀ {p q : Player}, -p ≠ q ↔ p = q := by decide
theorem neg_ne_self : ∀ (p : Player), -p ≠ p := by decide
theorem self_ne_neg : ∀ (p : Player), p ≠ -p := by decide

instance : InvolutiveNeg Player where
  neg_neg := by decide

/--
The multiplication of `Player`s is used to state the lemmas about the multiplication of
combinatorial games, such as `IGame.mulOption_mem_moves_mul`.
-/
instance : Mul Player where mul
  | left, p => p
  | right, p => -p

@[simp, grind =] lemma left_mul (p : Player) : left * p = p := rfl
@[simp, grind =] lemma right_mul (p : Player) : right * p = -p := rfl
@[simp, grind =] lemma mul_left : ∀ p, p * left = p := by decide
@[simp, grind =] lemma mul_right : ∀ p, p * right = -p := by decide
@[simp, grind =] lemma mul_self : ∀ p, p * p = left := by decide

instance : HasDistribNeg Player where
  neg_mul := by decide
  mul_neg := by decide

instance : CommGroup Player where
  one := left
  inv := id
  mul_assoc := by decide
  mul_comm := by decide
  one_mul := by decide
  mul_one := by decide
  inv_mul_cancel := by decide

@[simp, grind =] lemma one_eq_left : 1 = left := rfl
@[simp, grind =] lemma inv_eq_self (p : Player) : p⁻¹ = p := rfl

section Order
variable {α : Type*} {p : Player}

/-!
There are various constructions in game theory where the left and right sets are defined by
inequalities in opposite directions. The following definitions are designed to be used in these
situations.
-/

/-- `Player.le left x y` means `x ≤ y`, while `Player.le right x y` means `y ≤ x`. -/
protected abbrev le [LE α] (p : Player) : α → α → Prop :=
  p.cases (· ≤ ·) (· ≥ ·)

/-- `Player.lt left x y` means `x < y`, while `Player.le right x y` means `y < x`. -/
protected abbrev lt [LT α] (p : Player) : α → α → Prop :=
  p.cases (· < ·) (· > ·)

theorem le_left [LE α] {x y : α} : left.le x y ↔ x ≤ y := .rfl
theorem le_right [LE α] {x y : α} : right.le x y ↔ y ≤ x := .rfl
theorem lt_left [LT α] {x y : α} : left.lt x y ↔ x < y := .rfl
theorem lt_right [LT α] {x y : α} : right.lt x y ↔ y < x := .rfl

@[simp] theorem le_neg [LE α] {x y : α} : (-p).le x y ↔ p.le y x := by cases p <;> rfl
@[simp] theorem lt_neg [LE α] {x y : α} : (-p).le x y ↔ p.le y x := by cases p <;> rfl

@[refl]
theorem le.rfl [Preorder α] {x : α} : p.le x x := by
  cases p <;> exact le_rfl

theorem le.trans [Preorder α] {x y z : α} (h₁ : p.le x y) (h₂ : p.le y z) : p.le x z :=
  match p with
  | left => le_trans h₁ h₂
  | right => le_trans h₂ h₁

theorem lt.trans [Preorder α] {x y z : α} (h₁ : p.lt x y) (h₂ : p.lt y z) : p.lt x z :=
  match p with
  | left => lt_trans h₁ h₂
  | right => lt_trans h₂ h₁

@[gcongr]
theorem le_congr {x₁ x₂ y₁ y₂ : α} [Preorder α]
    (h₁ : AntisymmRel (· ≤ ·) x₁ x₂) (h₂ : AntisymmRel (· ≤ ·) y₁ y₂) :
    p.le x₁ y₁ ↔ p.le x₂ y₂ := by
  cases p with
  | left => exact h₁.le_congr h₂
  | right => exact h₂.le_congr h₁

@[gcongr]
theorem lt_congr {x₁ x₂ y₁ y₂ : α} [Preorder α]
    (h₁ : AntisymmRel (· ≤ ·) x₁ x₂) (h₂ : AntisymmRel (· ≤ ·) y₁ y₂) :
    p.lt x₁ y₁ ↔ p.lt x₂ y₂ := by
  cases p with
  | left => exact h₁.lt_congr h₂
  | right => exact h₂.lt_congr h₁

end Order

end Player

open Player

/-! ### OfSets -/

/--
Type class for the `ofSets` operation.
Used to implement the `!{st}` and `!{s | t}` syntax.
-/
class OfSets (α : Type (u + 1)) (Valid : outParam ((Player → Set α) → Prop)) where
  /-- Construct a combinatorial game from its left and right sets. -/
  ofSets (st : Player → Set α) (h : Valid st) [Small.{u} (st left)] [Small.{u} (st right)] : α
export OfSets (ofSets)

@[inherit_doc OfSets.ofSets]
macro "!{" st:term "}'" h:term:max : term => `(OfSets.ofSets $st $h)

@[inherit_doc OfSets.ofSets]
macro "!{" s:term " | " t:term "}'" h:term:max : term => `(!{Player.cases $s $t}'$h)

macro "of_sets_tactic" : tactic =>
  `(tactic| first
    | done
    | trivial
    | assumption
    | aesop
    | fail "failed to prove sets are valid, try to use `!{st}'h` notation instead, \
where `h` is a proof that sets are valid"
   )

@[inherit_doc OfSets.ofSets]
macro:max "!{" st:term "}" : term => `(!{$st}'(by of_sets_tactic))

@[inherit_doc OfSets.ofSets]
macro:max "!{" s:term " | " t:term "}" : term => `(!{$s | $t}'(by of_sets_tactic))

recommended_spelling "ofSets" for "!{st}'h" in [ofSets, «term!{_}'_»]
recommended_spelling "ofSets" for "!{s | t}'h" in [ofSets, «term!{_|_}'_»]
recommended_spelling "ofSets" for "!{st}" in [ofSets, «term!{_}»]
recommended_spelling "ofSets" for "!{s | t}" in [ofSets, «term!{_|_}»]

open Lean PrettyPrinter Delaborator SubExpr in
@[app_delab OfSets.ofSets]
def delabOfSets : Delab := do
  let e ← getExpr
  guard <| e.isAppOfArity' ``OfSets.ofSets 7
  withNaryArg 3 do
    let e ← getExpr
    if e.isAppOfArity' ``Player.cases 3 then
      let s ← withNaryArg 1 delab
      let t ← withNaryArg 2 delab
      `(!{$s | $t})
    else
      let st ← delab
      `(!{$st})

theorem ofSets_eq_ofSets_cases {α} {Valid : (Player → Set α) → Prop} [OfSets α Valid]
    (st : Player → Set α) (h : Valid st) [Small (st left)] [Small (st right)] :
    !{st} = !{st left | st right}'(by convert h; aesop) := by
  congr; ext1 p; cases p <;> rfl
