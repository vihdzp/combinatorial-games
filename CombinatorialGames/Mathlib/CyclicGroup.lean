import Mathlib.GroupTheory.SpecificGroups.Cyclic

-- #34629

open AddSubgroup Subgroup

variable {G : Type*}

/-- The isomorphism from `ZMod n` to any additive group of `Nat.card` equal to `n`
generated by a single element `g` which sends `1` to `g`. -/
noncomputable def zmodAddEquivOfGeneratorOfCardEq [AddGroup G] {g : G} (hg : ∀ x, x ∈ zmultiples g)
    {n : ℕ} (hn : Nat.card G = n) : ZMod n ≃+ G :=
  have kereq : ((zmultiplesHom G) g).ker = zmultiples (n : ℤ) := by
    rw [zmultiplesHom_ker_eq, ← Nat.card_zmultiples, ← hn,
      Nat.card_congr (Equiv.subtypeUnivEquiv hg)]
  Int.quotientZMultiplesNatEquivZMod n
    |>.symm.trans <| QuotientAddGroup.quotientAddEquivOfEq kereq
    |>.symm.trans <| QuotientAddGroup.quotientKerEquivOfSurjective (zmultiplesHom G g) hg

@[simp]
theorem zmodAddEquivOfGeneratorOfCardEq_apply_intCast [AddGroup G]
    {g : G} (hg : ∀ x, x ∈ zmultiples g) {n : ℕ} (hn : Nat.card G = n)
    (i : ℤ) : zmodAddEquivOfGeneratorOfCardEq hg hn i = i • g := by
  change (ZMod.cast (i : ZMod n) : ℤ) • g = i • g
  rw [ZMod.coe_intCast, Int.emod_def, eq_comm, ← sub_eq_zero, sub_eq_add_neg, ← sub_zsmul,
    Int.sub_sub_self, mul_zsmul', natCast_zsmul, ← hn, card_nsmul_eq_zero', zsmul_zero]

@[simp]
theorem zmodAddEquivOfGeneratorOfCardEq_symm_apply_zsmul [AddGroup G]
    {g : G} (hg : ∀ x, x ∈ zmultiples g) {n : ℕ} (hn : Nat.card G = n)
    (i : ℤ) : (zmodAddEquivOfGeneratorOfCardEq hg hn).symm (i • g) = i := by
  rw [AddEquiv.symm_apply_eq, zmodAddEquivOfGeneratorOfCardEq_apply_intCast]

@[simp]
theorem zmodAddEquivOfGeneratorOfCardEq_apply_one [AddGroup G]
    {g : G} (hg : ∀ x, x ∈ zmultiples g) {n : ℕ} (hn : Nat.card G = n) :
    zmodAddEquivOfGeneratorOfCardEq hg hn 1 = g := by
  simpa using zmodAddEquivOfGeneratorOfCardEq_apply_intCast hg hn 1

@[simp]
theorem zmodAddEquivOfGeneratorOfCardEq_symm_apply_generator [AddGroup G]
    {g : G} (hg : ∀ x, x ∈ zmultiples g) {n : ℕ} (hn : Nat.card G = n) :
    (zmodAddEquivOfGeneratorOfCardEq hg hn).symm g = 1 := by
  simpa using zmodAddEquivOfGeneratorOfCardEq_symm_apply_zsmul hg hn 1

/-- The isomorphism from `Multiplicative (ZMod n)` to any multiplicative group
of `Nat.card` equal to `n` generated by a single element `g` which sends
`Multiplicative.ofAdd 1` to `g`. -/
noncomputable def zmodMulEquivOfGeneratorOfCardEq [Group G] {g : G} (hg : ∀ x, x ∈ zpowers g)
    {n : ℕ} (hn : Nat.card G = n) : Multiplicative (ZMod n) ≃* G :=
  AddEquiv.toMultiplicative <| zmodAddEquivOfGeneratorOfCardEq (G := Additive G) hg hn

@[simp]
theorem zmodMulEquivOfGeneratorOfCardEq_apply_ofAdd_intCast [Group G]
    {g : G} (hg : ∀ x, x ∈ zpowers g) {n : ℕ} (hn : Nat.card G = n)
    (i : ℤ) : zmodMulEquivOfGeneratorOfCardEq hg hn (Multiplicative.ofAdd i) = g ^ i :=
  zmodAddEquivOfGeneratorOfCardEq_apply_intCast (G := Additive G) hg hn i

@[simp]
theorem zmodMulEquivOfGeneratorOfCardEq_symm_apply_zpow [Group G]
    {g : G} (hg : ∀ x, x ∈ zpowers g) {n : ℕ} (hn : Nat.card G = n)
    (i : ℤ) : (zmodMulEquivOfGeneratorOfCardEq hg hn).symm (g ^ i) =
      Multiplicative.ofAdd (i : ZMod n) :=
  zmodAddEquivOfGeneratorOfCardEq_symm_apply_zsmul (G := Additive G) hg hn i

@[simp]
theorem zmodMulEquivOfGeneratorOfCardEq_apply_ofAdd_one [Group G]
    {g : G} (hg : ∀ x, x ∈ zpowers g) {n : ℕ} (hn : Nat.card G = n) :
    zmodMulEquivOfGeneratorOfCardEq hg hn (Multiplicative.ofAdd 1) = g :=
  zmodAddEquivOfGeneratorOfCardEq_apply_one (G := Additive G) hg hn

@[simp]
theorem zmodMulEquivOfGeneratorOfCardEq_symm_apply_generator [Group G]
    {g : G} (hg : ∀ x, x ∈ zpowers g) {n : ℕ} (hn : Nat.card G = n) :
    (zmodMulEquivOfGeneratorOfCardEq hg hn).symm g = Multiplicative.ofAdd 1 :=
  zmodAddEquivOfGeneratorOfCardEq_symm_apply_generator (G := Additive G) hg hn
